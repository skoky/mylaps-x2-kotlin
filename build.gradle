buildscript {
    ext.kotlin_version = '1.3.50'
    repositories { jcenter() }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }

}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.50' // or any other kotlin plugin
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.50'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

kotlin {

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.12.0'
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        linuxX64Main {
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.12.0'
            }
        }
    }

    linuxX64() {
        
        compilations.main.cinterops {
            mylaps {
                includeDirs("sdk-master/include")
            }
            zmq {}
        }

        binaries {
            executable {
                baseName = 'x2'
                linkerOpts = ["-L${projectDir.path}/sdk-master/lib/linux/x86-64".toString(), '-lmylapssdk', '-L/usr/lib/x86_64-linux-gnu', '-lzmq']
                println("Executable path: ${outputFile.absolutePath}".toString())
            }
        }
    }

    mingwX64("windowsX64") {
        compilations.main.cinterops {
            mylaps {
                includeDirs("sdk-master/include")
            }
        }

        binaries {
            executable {
                baseName = 'x2'
                linkerOpts = ["-L${projectDir.path}/sdk-master/lib/windows/x64", '-lMylapsSDK']
                println("Executable path: ${outputFile.absolutePath}")
            }
        }
    }
}
